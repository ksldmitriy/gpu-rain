#version 460

struct Droplet {
  vec3 pos;
  float splash_time;
  vec2 top_down_uv;
};

layout(std430, binding = 0) buffer DropletsSSBO {
  Droplet droplets_arr[];
};

layout(binding = 1, offset = 0) uniform atomic_uint acbo;

// constants
const uint UINT_MAX = ~uint(0);

// uniforms
uniform int u_drops_count = 0;
uniform float u_kill_plane;
uniform float u_top_down_camera_y;
uniform float u_speed = 0;
uniform float u_delta_time = 0;

uniform vec3 u_spawn_nl;
uniform vec3 u_spawn_nr;
uniform vec3 u_spawn_fl;
uniform vec3 u_spawn_fr;

uniform vec2 u_spawn_nl_uv;
uniform vec2 u_spawn_nr_uv;
uniform vec2 u_spawn_fl_uv;
uniform vec2 u_spawn_fr_uv;

uniform float u_spawn_ratio;

uniform float u_spawn_y_range;

uniform float u_splash_duration;

uniform sampler2D u_top_down_depth_texture;

// logic
void move_droplet(inout Droplet drop);
void respawn_droplet(inout Droplet drop);
bool is_colliding(inout Droplet drop, out float surface_height);
float random_float(uint seed);
uint pcg_hash(uint seed);

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;
void main() {
  uint index = gl_GlobalInvocationID.x;
  if (index >= u_drops_count) {
    return;
  }

  if (droplets_arr[index].splash_time == 0) { // falling drop
    float surface_height;
    is_colliding(droplets_arr[index], surface_height);

    surface_height = max(surface_height, u_kill_plane);

    if (droplets_arr[index].pos.y < u_kill_plane && false) {
      respawn_droplet(droplets_arr[index]);
      return;
    }

    if (surface_height > droplets_arr[index].pos.y) {
      droplets_arr[index].pos.y = surface_height;
      droplets_arr[index].splash_time = 0.0001;
      return;
    }

    move_droplet(droplets_arr[index]);

  } else { // drop hitting surface
    droplets_arr[index].splash_time += u_delta_time / u_splash_duration;

    if (droplets_arr[index].splash_time > 1) {
      respawn_droplet(droplets_arr[index]);
    }
  }
}

void move_droplet(inout Droplet drop) {
  drop.pos.y -= u_speed * u_delta_time;
}

void respawn_droplet(inout Droplet drop) {
  uint seed = atomicCounterAdd(acbo, 4);

  float r_p = random_float(seed + 0);

  float r1 = random_float(seed + 1);
  float r2 = random_float(seed + 2);
  float r3 = random_float(seed + 3);

  vec3 p3;
  vec2 p3_uv;
  if (r_p < u_spawn_ratio) {
    p3 = u_spawn_fl;
    p3_uv = u_spawn_fl_uv;
  } else {
    p3 = u_spawn_nr;
    p3_uv = u_spawn_nr_uv;
  }

  float sqrt_r1 = sqrt(r1);
  float a = 1.f - sqrt_r1;
  float b = sqrt_r1 * (1.f - r2);
  float c = sqrt_r1 * r2;

  drop.pos = (u_spawn_nl * a) + (p3 * b) + (u_spawn_fr * c);
  drop.pos.y += u_spawn_y_range * r3;

  drop.top_down_uv = (u_spawn_nl_uv * a) + (p3_uv * b) + (u_spawn_fr_uv * c);

  drop.splash_time = 0;
}

bool is_colliding(inout Droplet drop, out float surface_height) {
  surface_height = u_top_down_camera_y -
                   texture(u_top_down_depth_texture, drop.top_down_uv).x;

  return drop.pos.y < surface_height;
}

float random_float(uint seed) {
  uint hash = pcg_hash(seed);
  return float(hash) / float(UINT_MAX);
}

uint pcg_hash(uint seed) {
  uint state = seed * 747796405u + 2891336453u;
  uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
  return (word >> 22u) ^ word;
}
